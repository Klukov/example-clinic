plugins {
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'groovy'
    id 'pmd'
    id 'com.diffplug.spotless' version '6.23.3'
}

group = 'org.klukov.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.flywaydb:flyway-core:9.22.3' // update to 10+ when compatibility issues will be resolved
    runtimeOnly 'org.postgresql:postgresql:42.7.1'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-3.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-3.0'
    testImplementation 'org.codehaus.groovy:groovy:3.0.20'
    testImplementation 'org.testcontainers:spock:1.19.5'
    testImplementation 'org.testcontainers:postgresql:1.19.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat().aosp()
        formatAnnotations()
    }
    groovy {
        importOrder()
        indentWithSpaces()
        endWithNewline()
        trimTrailingWhitespace()
        excludeJava()
    }
    sql {
        target 'src/main/resources/**/*.sql'
        dbeaver()
    }
}

pmd {
    ignoreFailures = false
}
