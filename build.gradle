plugins {
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'groovy'
    id 'pmd'
    id 'com.diffplug.spotless' version '7.0.2'
}

group = 'org.klukov.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.flywaydb:flyway-core:9.22.3' // update to 10+ when compatibility issues will be resolved
    runtimeOnly 'org.postgresql:postgresql:42.7.5'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    implementation 'org.slf4j:slf4j-api:2.0.16'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:2.4-M5-groovy-3.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M5-groovy-3.0'
    testImplementation 'org.codehaus.groovy:groovy:3.0.23'
    testImplementation 'org.testcontainers:spock:1.20.4'
    testImplementation 'org.testcontainers:postgresql:1.20.4'
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat().aosp()
        formatAnnotations()
    }
    groovy {
        importOrder()
        leadingTabsToSpaces()
        endWithNewline()
        trimTrailingWhitespace()
        excludeJava()
    }
    sql {
        target 'src/main/resources/**/*.sql'
        dbeaver()
    }
}

pmd {
    ignoreFailures = false
}
